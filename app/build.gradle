apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "cz.covid19cz.erouska"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.commitCount()
        versionName rootProject.ext.versionName
        archivesBaseName = "covid19-cz"
        multiDexEnabled true
        // If we support another language, add it here
        def supportedLanguages = ["en", "cs", "ro", "ru", "sk", "uk", "vi"]
        resConfigs supportedLanguages
        buildConfigField "String[]", "SUPPORTED_LANGUAGES", "{\"" + supportedLanguages.join("\",\"") + "\"}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    flavorDimensions "environment"
    testBuildType "debug"

    productFlavors {
        dev {
            dimension "environment"
            applicationIdSuffix ".dev"
        }
        prod {
            dimension "environment"
        }
    }

    signingConfigs {
        debug {
            storeFile file("../meta/debug.keystore")
        }
        release {
            storeFile file(System.getenv("EROUSKA_RELEASE_KEYSTORE_PATH") ?: "No CI")
            storePassword System.getenv("EROUSKA_RELEASE_KEYSTORE_PASSWORD")
            keyAlias "covid19cz"
            keyPassword System.getenv("EROUSKA_RELEASE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding = true
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.3.5"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.3.7'

    // Android Basics
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation 'androidx.fragment:fragment:1.2.5'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-rc1'
    implementation "com.google.android.material:material:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.browser:browser:1.2.0"
    implementation "com.google.android.play:core-ktx:1.8.0"
    implementation "androidx.work:work-runtime-ktx:2.4.0"

    // Arch
    implementation project(':arch')

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.0"

    // Koin for Android
    implementation "org.koin:koin-android:2.0.1"
    implementation "org.koin:koin-androidx-scope:2.0.1"
    implementation "org.koin:koin-androidx-viewmodel:2.0.1"

    //RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.17"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    //RxPermisssions
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'

    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // Firebase
    implementation 'com.google.firebase:firebase-auth:19.3.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.firebase:firebase-config-ktx:19.2.0'
    implementation 'com.google.firebase:firebase-functions-ktx:19.0.2'
    implementation 'com.google.firebase:firebase-storage-ktx:19.1.1'
    implementation 'com.google.firebase:firebase-messaging:20.2.4'

    // Play Services
    implementation 'com.google.android.play:core:1.8.0'
    implementation 'com.google.android.gms:play-services-base:17.3.0'
    implementation 'com.google.android.gms:play-services-basement:17.3.0'
    implementation 'com.google.android.gms:play-services-safetynet:17.0.0'
    implementation 'com.google.android.gms:play-services-tasks:17.1.0'

    // Room
    implementation "androidx.room:room-runtime:2.2.5"
    implementation "androidx.room:room-rxjava2:2.2.5"
    implementation "androidx.room:room-ktx:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"

    // Markdown
    implementation "io.noties.markwon:core:4.3.1"
    implementation 'io.noties.markwon:image-glide:4.3.1'
    implementation 'com.atlassian.commonmark:commonmark-ext-autolink:0.12.1'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.7.2'

    // Others
    implementation 'com.android.support:customtabs:28.0.0'
    implementation 'com.jaredrummler:android-device-names:2.0.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'
    implementation 'net.lingala.zip4j:zip4j:2.6.1'

    // Tests
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'org.awaitility:awaitility:3.1.6'

    androidTestImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation "org.koin:koin-test:2.0.1"
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestUtil 'androidx.test:orchestrator:1.2.0'

    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'
    debugImplementation 'com.idescout.sql:sqlscout-server:4.1'
    releaseImplementation 'com.idescout.sql:sqlscout-server-noop:4.1'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
