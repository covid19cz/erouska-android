apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "cz.covid19cz.erouska"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.commitCount()
        versionName rootProject.ext.versionName
        archivesBaseName = "covid19-cz"
        multiDexEnabled true
        // If we support another language, add it here
        def supportedLanguages = ["cs", "en"]
        resConfigs supportedLanguages
        buildConfigField "String[]", "SUPPORTED_LANGUAGES", "{\"" + supportedLanguages.join("\",\"") + "\"}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    flavorDimensions "environment"
    testBuildType "debug"

    productFlavors {
        dev {
            dimension "environment"
            applicationIdSuffix ".dev"
        }
        prod {
            dimension "environment"
        }
    }

    signingConfigs {
        debug {
            storeFile file("../meta/debug.keystore")
        }
        release {
            storeFile file(System.getenv("EROUSKA_RELEASE_KEYSTORE_PATH") ?: "No CI")
            storePassword System.getenv("EROUSKA_RELEASE_KEYSTORE_PASSWORD")
            keyAlias "covid19cz"
            keyPassword System.getenv("EROUSKA_RELEASE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$versions.coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.1.1'

    // Android Basics
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation 'androidx.fragment:fragment:1.2.3'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.browser:browser:1.2.0"

    // Arch
    implementation project(':arch')

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.nav"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.nav"

    // Koin for Android
    implementation "org.koin:koin-android:$versions.koin"
    implementation "org.koin:koin-androidx-scope:$versions.koin"
    implementation "org.koin:koin-androidx-viewmodel:$versions.koin"

    //RxJava
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxjava"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxandroid"

    //RxPermisssions
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'

    // BLE Scanner Compat
    implementation 'no.nordicsemi.android.support.v18:scanner:1.4.2'

    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // Firebase
    implementation 'com.google.firebase:firebase-auth:19.3.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.firebase:firebase-config-ktx:19.1.3'
    implementation 'com.google.firebase:firebase-functions-ktx:19.0.2'
    implementation 'com.google.firebase:firebase-storage-ktx:19.1.1'
    implementation 'com.google.firebase:firebase-messaging:20.1.3'

    // Room
    implementation "androidx.room:room-runtime:$versions.room"
    implementation "androidx.room:room-rxjava2:$versions.room"
    kapt "androidx.room:room-compiler:$versions.room"

    // Markdown
    implementation "io.noties.markwon:core:4.3.1"
    implementation 'io.noties.markwon:image-glide:4.3.1'
    implementation 'com.atlassian.commonmark:commonmark-ext-autolink:0.12.1'

    // Others
    implementation "net.sf.supercsv:super-csv:$versions.super_csv"
    implementation 'com.android.support:customtabs:28.0.0'
    implementation 'com.jaredrummler:android-device-names:2.0.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.3'

    // Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.awaitility:awaitility:3.1.6'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestUtil 'androidx.test:orchestrator:1.2.0'

    debugImplementation 'com.idescout.sql:sqlscout-server:4.1'
    releaseImplementation 'com.idescout.sql:sqlscout-server-noop:4.1'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
